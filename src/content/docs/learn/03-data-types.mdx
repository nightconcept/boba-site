---
title: "Data Types"
description: "Explore the fundamental data types in Boba for handling numbers, text, and true/false values."
---

import { Aside } from '@astrojs/starlight/components';

In the last chapter, you learned how to create variables and constants. Now, let's explore the fundamental data types you'll use to store different kinds of information in Boba.

## Primitive Types

Boba has a set of primitive types that are the building blocks of data in your programs.

### `int`

The `int` type is used to represent a 64-bit signed integer (a whole number). It is the ideal type for counting, indexing into collections, representing IDs, and any situation where fractional values are not needed.
```boba
let score: int = 100
let quantity: int = 3
let current_level = 10 // Inferred as int
```

##### Writing Numbers in Different Bases

In addition to standard decimal (base-10) numbers, Boba supports hexadecimal and binary notations, which are common in systems programming, graphics, and data manipulation.

- **Hexadecimal (base-16):** Prefix the number with `0x`. Uses digits `0-9` and letters `a-f`.
- **Binary (base-2):** Prefix the number with `0b`. Uses only digits `0` and `1`.

No matter how you write the number, it's still just an `int` with the same value.
```boba
// All of these variables hold the exact same integer value: 255
let decimal_value: int = 255
let hex_value: int = 0xFF
let binary_value: int = 0b11111111

print(decimal_value == hex_value) // true
print(hex_value == binary_value) // true
```

##### Improving Readability with `_`

Long number literals can be difficult to read. Boba allows you to use an underscore `_` as a visual separator anywhere inside a number. The compiler completely ignores it.

This works for `int` and `float` types, as well as for hex and binary notations.
```boba
// These are easier to read thanks to the '_' separator.
let one_billion: int = 1_000_000_000
let pi: float = 3.141_592_653

// This is especially useful for hex and binary values.
let packed_color: int = 0xFF_AB_00_FF
let permission_flags: int = 0b1010_1111
```

### `float`

The `float` type is used to represent a 64-bit double-precision floating-point number. It is the correct type for scientific calculations, measurements, or any value that may have a fractional component.
```boba
let price: float = 19.99
let PI: float = 3.14159
var temperature = 25.5 // Inferred as float
```
Note: While floats are powerful, be mindful of standard precision limitations inherent to floating-point arithmetic.

### `string`

The `string` type is used to represent text.

```boba
var message: string = "Hello, Boba!"
var playerName = "Ada" // Inferred as a string
```
##### Formatting Strings with F-Strings

> To embed variables and expressions directly into a string, Boba uses **f-strings**. An f-string is a string literal prefixed with the letter `f`. This explicitly tells the compiler to process any expressions inside curly braces `{}`.

```boba
let player_name = "Ada"
let level: int = 10

// Use an f-string to create a formatted message.
let welcome_message = f"Welcome, {player_name} (Level {level})!" 
print(welcome_message) // Outputs: Welcome, Ada (Level 10)!

// Regular strings do not process the braces.
let template = "This is a template: {some_value}"
print(template) // Outputs: This is a template: {some_value}
```

### `boolean`

The `boolean` type represents a value that can be either `true` or `false`. Booleans are essential for controlling the flow of your program.

```boba
var isGameOver: boolean = false
var hasKey = true // Inferred as a boolean
```

## Composite Types

Boba also has composite types, which are made up of primitive types.

### `list`

A `list` is an ordered collection of values of the same type. Lists are dynamic, meaning you can add or remove items.

```boba
// A list of numbers
var highScores: int[] = [100, 95, 80]

// A list of strings
var ingredients: string[] = ["flour", "sugar", "boba pearls"]
```

You can perform many operations on lists, like getting their size or adding new items.

```boba
print(ingredients.len()) // Outputs: 3

// Add a new item to the end
ingredients.push("tapioca pearls")
print(ingredients.len()) // Outputs: 4

// Access items by their index (starting from 0)
print(ingredients[0]) // Outputs: "flour"
```

### `map`

A `map` is a collection of key-value pairs. Each key must be unique, and all keys must be of the same type, as must all values.

```boba
// Create a map with string keys and number values
var scores: [string:int] = {
  "ada": 100,
  "grace": 95
}

// Check its initial size
print(scores.len()) // Outputs: 2

// Insert a new key-value pair
scores["boba"] = 105

// Check the new size
print(scores.len()) // Outputs: 3
```

Accessing a key in a map returns a special `Option` type to safely handle cases where the key might not exist. We will cover this powerful feature in a later chapter.

In the next chapter, we'll look at how to use these types with operators to perform calculations and make comparisons.
